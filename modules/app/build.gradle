import java.util.regex.Pattern
import java.util.stream.Collectors

buildscript {
  ext {
    springBootVersion = '1.5.6.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

plugins {
  id "org.sonarqube" version "2.5"
}

apply plugin: 'checkstyle'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.sonarqube'

jar {
  baseName = 'tcrs'
  version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}


configurations {
  compile.exclude module: 'spring-boot-starter-logging'
}
dependencies {
  compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
  compile 'io.springfox:springfox-swagger2:2.7.0'
  compile 'io.springfox:springfox-swagger-ui:2.7.0'
  compile 'mysql:mysql-connector-java:5.1.6'
  compile 'org.apache.commons:commons-lang3:3.6'
  compile 'org.apache.commons:commons-text:1.1'
  compile 'org.modelmapper:modelmapper:0.7.5'
  compile 'org.springframework.boot:spring-boot-devtools'
  compile 'org.springframework.boot:spring-boot-starter-cache'
  compile 'org.springframework.boot:spring-boot-starter-data-jpa'
  compile 'org.springframework.boot:spring-boot-starter-log4j2'
  compile 'org.springframework.boot:spring-boot-starter-mail'
  compile 'org.springframework.boot:spring-boot-starter-security'
  compile 'org.springframework.boot:spring-boot-starter-web'
//  compile 'org.springframework.session:spring-session'

  compileOnly "org.projectlombok:lombok:1.16.10"

  testCompile 'org.powermock:powermock-mockito-release-full:1.6.4'
  testCompile 'org.springframework.boot:spring-boot-starter-test'
}

idea {
  module {
    downloadSources = true
    excludeDirs += file("bundle")
    excludeDirs += file("src/main/webapp")
  }
}

def fetchIgnoreInTestCoverageFiles = { List<Pattern> ignoreInTestCoverageFlags ->
  def lengthCwd = projectDir.getAbsolutePath().length()

  def containsFlag = { File file ->
    ignoreInTestCoverageFlags.stream().filter({(file.text =~ it).find()}).findFirst().isPresent()
  }

  sourceSets.main.allJava.files.stream()
    .filter(containsFlag)
    .map({it.getAbsolutePath().substring(lengthCwd + 1)})
    .collect(Collectors.toList())
}

sonarqube {
  properties {
    property 'sonar.projectKey', 'com.hatorach.tcrs'
    property 'sonar.projectName', 'TCRS'
    property 'sonar.exclusions', fetchIgnoreInTestCoverageFiles([/@IgnoreInTestCoverage/, /@Data/])
  }
}

checkstyle {
  toolVersion = "7.8.2"
  ignoreFailures = false
  maxWarnings = 0
}
